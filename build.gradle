plugins {
    id 'java'
}

allprojects {
    group = 'com.github.frosxtt'
    version = '1.0.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
        withSourcesJar()
        withJavadocJar()
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    test {
        useJUnitPlatform()
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                
                pom {
                    name = project.name
                    description = 'A zero-dependency, high-performance configuration library for Java.'
                    url = 'https://github.com/frosxt/JsonConfig'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                }
            }
        }
    }
}
